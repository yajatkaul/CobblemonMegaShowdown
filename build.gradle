plugins {
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'org.jetbrains.kotlin.jvm' version '2.2.10'
    id 'co.uzzu.dotenv.gradle'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    base {
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        mavenCentral()
        maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
        maven { url 'https://maven.impactdev.net/repository/development/' }
        maven { url 'https://maven.neoforged.net/releases' }
        maven { url 'https://thedarkcolour.github.io/KotlinForForge/' }

        maven { url 'https://maven.wispforest.io/releases' }
        maven { url 'https://maven.su5ed.dev/releases' }
        maven { url 'https://maven.fabricmc.net' }
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
        }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        repositories {

        }
    }
}

Properties dotenv = new Properties()
File envFile = file(".env")

if (envFile.exists()) {
    envFile.withInputStream { stream ->
        dotenv.load(stream)
    }
}

def resolveEnv = { String key ->
    String value = dotenv.getProperty(key)
    return value != null ? value : System.getenv(key)
}

publishMods {
    changelog = file("CHANGELOG.md").text
    type = STABLE

    String modrinthToken = resolveEnv("MODRINTH_TOKEN")
    String curseforgeToken = resolveEnv("CURSEFORGE_TOKEN")
    String discordWebhook = resolveEnv("DISCORD_WEBHOOK")

    dryRun = modrinthToken == null || curseforgeToken == null || discordWebhook == null

    def cfOptions = curseforgeOptions {
        accessToken = curseforgeToken
        projectId = project.curseforge_id
        clientRequired = true
        serverRequired = true
        minecraftVersions.add("1.21.1")
        projectSlug = "cobblemon-mega-showdown"
    }

    def mrOptions = modrinthOptions {
        accessToken = modrinthToken
        projectId = project.modrinth_id
        minecraftVersions.add("1.21.1")
    }

    curseforge("curseforgeFabric") {
        from cfOptions
        file project(":fabric")
        modLoaders.add("fabric")
        requires("fabric-api")
        requires("cobblemon")
        requires("accessories")
        requires("architectury-api")
        requires("owo-lib")
    }
    modrinth("modrinthFabric") {
        from mrOptions
        file project(":fabric")
        modLoaders.add("fabric")
        requires("fabric-api")
        requires("cobblemon")
        requires("accessories")
        requires("architectury-api")
        requires("owo-lib")
    }

    curseforge("curseforgeNeoForge") {
        from cfOptions
        file project(":neoforge")
        modLoaders.add("neoforge")
        requires("cobblemon")
        requires("accessories")
        requires("architectury-api")
        requires("owo-lib")
    }
    modrinth("modrinthNeoForge") {
        from mrOptions
        file project(":neoforge")
        modLoaders.add("neoforge")
        requires("cobblemon")
        requires("accessories")
        requires("architectury-api")
        requires("owo-lib")
    }

    discord {
        webhookUrl = discordWebhook
        dryRunWebhookUrl = discordWebhook
        username = "Mega Showdown Changelog"
        avatarUrl = "https://cdn.modrinth.com/data/SszvX85I/62338ad15814a49df76dfbece1edba24ebca4135.png"
        content = changelog.map { "# A new version has been released! \n" + it}

        style {
            look = "MODERN"
            thumbnailUrl = "https://cdn.modrinth.com/data/SszvX85I/62338ad15814a49df76dfbece1edba24ebca4135.png"
            color = "#266e10"
        }
    }
}